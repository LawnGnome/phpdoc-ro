dnl $Id$

AC_INIT()

dnl used by checkdoc
AC_PATH_PROG(PERL_PATH, perl)

LANG=en
AC_SUBST(LANG)

ENCODING="ISO-8859-1"
AC_SUBST(ENCODING)

DOCBOOK_DOCTYPE="-//Norman Walsh//DTD DocBk XML V1.4//EN"
AC_SUBST(DOCBOOK_DOCTYPE)

AC_MSG_CHECKING(for docbook.dsl)
AC_ARG_WITH(dsssl,
[  --with-dsssl=[DIR]      Look for DSSSL stylesheets in the specified directory],
[
    if test -d "$withval" ; then
	DOCBOOK_HTML=$withval/html/docbook.dsl
        DOCBOOK_PRINT=$withval/print/docbook.dsl
        AC_MSG_RESULT(in $withval)
    fi
],[
    for dir in \
           /usr/lib/sgml/stylesheet/dsssl/docbook/nwalsh \
	   /usr/local/share/sgml/docbook/dsssl/modular \
	   /usr/lib/dsssl/stylesheets/docbook \
	   /usr/lib/dsssl/stylesheets/nwalsh-modular \
	   /usr/lib/dsssl/stylesheets/docbook \
	   /usr/lib/sgml/stylesheets/nwalsh-modular \
	   /usr/lib/sgml/docbook\
	   /usr/lib/sgml/stylesheets/docbook \
	   /opt/sgml/lib/stylesheets/docbook \
	   /usr/share/sgml/docbk30/dsl \
	   /usr/share/sgml/docbkdsl \
	   /usr/share/sgml/stylesheets/docbook \
           /usr/share/sgml/docbook/stylesheet/dsssl/modular \
           /usr/share/sgml/docbook/dsssl-stylesheets \
	   /usr/local/lib/dsssl/stylesheets/docbook \
	   /usr/local/lib/dsssl/stylesheets/nwalsh-modular \
	   /usr/local/lib/sgml/stylesheets/docbook \
	   /usr/local/lib/sgml/stylesheets/nwalsh-modular \
	   /usr/local/lib/sgml/docbook \
	   /usr/local/share/sgml/docbook/dsssl/modular \
	   ../phpdoc-tools/dsssl/docbook \
	   ../phpdoc-tools/dsssl \
	   phpdoc-tools/dsssl/docbook \
	   phpdoc-tools/dsssl
    do
	if test -f "$dir/html/docbook.dsl"; then
            DOCBOOK_HTML="$dir/html/docbook.dsl"
            DOCBOOK_PRINT="$dir/print/docbook.dsl"
            AC_MSG_RESULT(autodetected: $dir)
            break
	fi
    done
])
if test -z "$DOCBOOK_HTML"; then
    AC_MSG_ERROR(WARNING!!! DSSSL NOT FOUND - WON'T WORK THIS WAY)
fi
AC_SUBST(DOCBOOK_HTML)
AC_SUBST(DOCBOOK_PRINT)

AC_MSG_CHECKING(for docbook.xsl)
AC_ARG_WITH(xsl,
[  --with-xsl=[URI]        Look for XSL stylesheets at the specified URI],
[
    DOCBOOKXSL_BIGHTML=$withval/html/docbook.xsl
    DOCBOOKXSL_HTML=$withval/html/chunk.xsl
    DOCBOOKXSL_HTMLHELP=$withval/htmlhelp/htmlhelp.xsl
    DOCBOOKXSL_PRINT=$withval/fo/docbook.xsl
    AC_MSG_RESULT(in $withval)
],[
    for dir in \
        ../phpdoc-tools/xsl/docbook \
        ../phpdoc-tools/xsl \
        phpdoc-tools/xsl/docbook \
        phpdoc-tools/xsl
    do
        if test -d "$dir"; then
            DOCBOOKXSL_BIGHTML=$dir/html/docbook.xsl
            DOCBOOKXSL_HTML=$dir/html/chunk.xsl
            DOCBOOKXSL_HTMLHELP=$dir/htmlhelp/htmlhelp.xsl
            DOCBOOKXSL_PRINT=$dir/fo/docbook.xsl
            AC_MSG_RESULT(autodetected: $dir)
            
            break
        fi
    done
])
if test -z "$DOCBOOKXSL_BIGHTML"; then
    DOCBOOKXSL_BIGHTML=../phpdoc-tools/xsl/html/docbook.xsl
    DOCBOOKXSL_HTML=../phpdoc-tools/xsl/html/chunk.xsl
    DOCBOOKXSL_HTMLHELP=../phpdoc-tools/xsl/htmlhelp/htmlhelp.xsl
    DOCBOOKXSL_PRINT=../phpdoc-tools/xsl/fo/docbook.xsl
    AC_MSG_RESULT(not found, defaulting)
fi
AC_SUBST(DOCBOOKXSL_BIGHTML)
AC_SUBST(DOCBOOKXSL_HTML)
AC_SUBST(DOCBOOKXSL_HTMLHELP)
AC_SUBST(DOCBOOKXSL_PRINT)

dnl look for the OpenJade DSSSL parser
AC_PATH_PROG(OPENJADECHK, "openjade", no)
if test $OPENJADECHK = "no"; then
    dnl OpenJade isnt present, so look for the older Jade instead
    AC_PATH_PROG(JADECHK, "jade", no)
    if test $JADECHK = "no"; then
        AC_MSG_CHECKING(for jade in phpdoc-tools)
        if test -e ../phpdoc-tools/jade/jade.exe ; then
            JADEPATH=../phpdoc-tools/jade/jade.exe
            AC_MSG_RESULT("yes")
        else
            AC_MSG_RESULT("no")
            AC_MSG_ERROR(unable to locate either Jade or OpenJade)
        fi
    else
        JADEPATH=$JADECHK
    fi
else
    JADEPATH=$OPENJADECHK
fi

dnl Search for nsgmls (for win32 plug&pray)
if test -e ../phpdoc-tools/jade/nsgmls.exe ; then
    NSGMLSCMD=../phpdoc-tools/jade/nsgmls.exe
else
    NSGMLSCMD=nsgmls
fi

JADE=$JADEPATH
NSGMLS=$NSGMLSCMD

AC_SUBST(JADE)
AC_SUBST(NSGMLS)
AC_SUBST(HTMLHELP_ENCODING)

dnl localize order of number and element name
dnl in some headers autogenerated by jade
case "$LANG" in
  hu|ko) NUMBER_FIRST="#t" ;;
  *) NUMBER_FIRST="#f" ;;
esac
AC_SUBST(NUMBER_FIRST)

dnl Do something smart to find catalog-files
CATALOG=""

dnl SuSE 6.2/6.3 need this
if test -e /usr/share/sgml/CATALOG.docbk30
then
  CATALOG="$CATALOG -c /usr/share/sgml/CATALOG.docbk30"
fi

dnl For windows (and maybe *nix too?) users lacking catalog-files

dnl jade catalog file
# how about using JADEPATH? You should replace the last 4 chars ('jade') with catalog
if test -e ../phpdoc-tools/jade/catalog
then
  CATALOG="$CATALOG -c ../phpdoc-tools/jade/catalog"
fi

dnl iso-ents catalog file
if test -e ../phpdoc-tools/iso-entities/catalog
then
  CATALOG="$CATALOG -c ../phpdoc-tools/iso-entities/catalog"
fi

dnl dsssl catalog file
if test -e ../phpdoc-tools/dsssl/docbook/catalog
then
  CATALOG="$CATALOG -c ../phpdoc-tools/dsssl/docbook/catalog"
fi

AC_SUBST(CATALOG)

AC_OUTPUT(Makefile html.dsl phpweb.dsl)

dnl sanity checks for SuSE 6.x
if test -e /usr/share/sgml/docbk30/docbook.dcl 
then
  if grep -n "NAMELEN.\+44" /usr/share/sgml/docbk30/docbook.dcl /dev/null
  then
    AC_MSG_WARN(please change this to e.g. 'NAMELEN 54' in /usr/share/sgml/docbk30/docbook.dcl )
  fi
  if grep -n "OMITTAG.\+NO" /usr/share/sgml/docbk30/docbook.dcl /dev/null
  then
    AC_MSG_WARN(please change this to 'OMITTAG YES' in /usr/share/sgml/docbk30/docbook.dcl )
  fi

dnl look for tex bug in print stylesheet
  if test -e /usr/share/sgml/docbkdsl/print/dbprint.dsl
  then 
    if test 2 -eq  `grep -c tex  /usr/share/sgml/docbkdsl/print/dbprint.dsl`
    then
      if fgrep -q 'country: (dsssl-country-code)' /usr/share/sgml/docbkdsl/print/dbprint.dsl
      then 	
        if ! grep -q ';;.*country:' /usr/share/sgml/docbkdsl/print/dbprint.dsl
	then
	  AC_MSG_WARN(to prevent a tex bug you should comment out )
	  AC_MSG_WARN('country: (dsssl-country-code)' )
	  AC_MSG_WARN(in /usr/share/sgml/docbkdsl/print/dbprint.dsl )
	  AC_MSG_WARN(or update your DSSSL-Stylesheets)
	  AC_MSG_WARN(to at least version 1.54)
	fi
      fi
    fi
  fi
fi
